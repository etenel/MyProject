import com.android.build.gradle.internal.api.BaseVariantOutputImpl
import java.time.LocalDateTime

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("com.google.devtools.ksp")
    id ("kotlin-kapt")
    id("kotlinx-serialization")
    id("androidx.navigation.safeargs.kotlin")
    alias(libs.plugins.hilt)
    id("android.aop")
}

android {
    namespace = "com.mvvm.demo"
    compileSdk = 34
    buildFeatures.buildConfig = true
    defaultConfig {
        applicationId = "com.mvvm.demo"
        minSdk = 24
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"
        resourceConfigurations += listOf("zh")
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }

    }
    //appç­¾å
    signingConfigs {
        create("release") {
            storeFile = rootProject.file("myProject.jks")
            storePassword = "123456"
            keyAlias = "myProject"
            keyPassword = "123456"
        }
    }
    buildTypes {
        debug {
            // æ—¥å¿—æ‰“å°å¼€å…³
            buildConfigField("Boolean", "LOG_ENABLE", "true")
            // æµ‹è¯•åŒ…ä¸‹çš„ BuglyId
            buildConfigField("String", "BUGLY_ID", "\"buglyId\"")
            // æµ‹è¯•æœåŠ¡å™¨çš„ä¸»æœºåœ°å€
            buildConfigField("String", "HOST_URL", "\"https://debug\"")
            applicationIdSuffix = ".debug"
            isJniDebuggable = true
            isDebuggable = true
            isShrinkResources = false
            isMinifyEnabled = false
            signingConfig = signingConfigs.getByName("debug")
            //æ¸…å•å ä½ç¬¦
            addManifestPlaceholders(mapOf("app_name" to "å®‰å“æŠ€æœ¯ä¸­å°Debugç‰ˆ"))
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters += mutableSetOf("armeabi-v7a", "x86_64")
            }
        }
        create("preview") {
            applicationIdSuffix = ""
            initWith(getByName("debug"))
            //æ¸…å•å ä½ç¬¦
            addManifestPlaceholders(mapOf("app_name" to "å®‰å“æŠ€æœ¯ä¸­å°previewç‰ˆ"))
        }

        release {
            // æ—¥å¿—æ‰“å°å¼€å…³
            buildConfigField("Boolean", "LOG_ENABLE", "false")
            // æµ‹è¯•åŒ…ä¸‹çš„ BuglyId
            buildConfigField("String", "BUGLY_ID", "\"id\"")
            // æµ‹è¯•æœåŠ¡å™¨çš„ä¸»æœºåœ°å€
            buildConfigField("String", "HOST_URL", "\"https://release\"")
            isMinifyEnabled = true
            isShrinkResources = true

            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig = signingConfigs.getByName("release")
            //æ¸…å•å ä½ç¬¦
            addManifestPlaceholders(mapOf("app_name" to "@string/app_name"))
            ndk {
                // armeabiï¼šä¸‡é‡‘æ²¹æ¶æ„å¹³å°ï¼ˆå ç”¨ç‡ï¼š0%ï¼‰
                // armeabi-v7aï¼šæ›¾ç»ä¸»æµçš„æ¶æ„å¹³å°ï¼ˆå ç”¨ç‡ï¼š10%ï¼‰
                // arm64-v8aï¼šç›®å‰ä¸»æµæ¶æ„å¹³å°ï¼ˆå ç”¨ç‡ï¼š95%ï¼‰
                //noinspection ChromeOsAbiSupport
                abiFilters += mutableSetOf("armeabi-v7a", "arm64-v8a")
            }
            // Ensure Baseline Profile is fresh for release builds.
            // baselineProfile.automaticGenerationDuringBuild = true
        }
        //ä½¿ç”¨benchmarkæµ‹è¯•ç‰ˆæœ¬
        create("benchmark") {
            // Enable all the optimizations from release build through initWith(release).
            initWith(getByName("release"))
            matchingFallbacks.add("release")
            // Debug key signing is available to everyone.
            signingConfig = signingConfigs.getByName("release")
            // Only use benchmark proguard rules
            proguardFiles("benchmark-rules.pro")
            isMinifyEnabled = true
            applicationIdSuffix = ".benchmark"
        }
    }
    packaging {
        resources {
            excludes.add("/META-INF/{AL2.0,LGPL2.1}")
        }
    }

    testOptions {
        unitTests {
            isIncludeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildToolsVersion = "34.0.0"

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    applicationVariants.all {
        outputs.all {
            val ver = defaultConfig.versionName
            val date = LocalDateTime.now()
            val time: String =
                "${date.year}_${date.month}_${date.dayOfMonth}-${date.hour}-${date.minute}"
            (this as BaseVariantOutputImpl).outputFileName =
                "${rootProject.name}_${ver.orEmpty()}_${buildType.name}${time}.apk"
        }
    }
}
ksp{

}
androidAopConfig {

    // enabled ä¸º false åˆ‡é¢ä¸å†èµ·ä½œç”¨ï¼Œé»˜è®¤ä¸å†™ä¸º true
    enabled = true
    // include ä¸è®¾ç½®é»˜è®¤å…¨éƒ¨æ‰«æï¼Œè®¾ç½®ååªæ‰«æè®¾ç½®çš„åŒ…åçš„ä»£ç 
    include(
        android.defaultConfig.applicationId.orEmpty(),
    )
    // exclude æ˜¯æ‰«ææ—¶æ’é™¤çš„åŒ…
    // å¯æ’é™¤ kotlin ç›¸å…³ï¼Œæé«˜é€Ÿåº¦
//    exclude ("kotlin.jvm", "kotlin.internal","kotlinx.coroutines.internal",
//        "kotlinx.coroutines.android")

    // verifyLeafExtends æ˜¯å¦å¼€å¯éªŒè¯å¶å­ç»§æ‰¿ï¼Œé»˜è®¤æ‰“å¼€ï¼Œå¦‚æœæ²¡æœ‰è®¾ç½® @AndroidAopMatchClassMethod çš„ type = MatchType.LEAF_EXTENDSï¼Œå¯ä»¥å…³é—­
    verifyLeafExtends = true
    //é»˜è®¤å…³é—­ï¼Œå¼€å¯åœ¨ Build æˆ– æ‰“åŒ…å å°†ä¼šç”Ÿæˆåˆ‡ç‚¹ä¿¡æ¯jsonæ–‡ä»¶åœ¨ app/build/tmp/cutInfo.json
    cutInfoJson = false
    //é»˜è®¤å¼€å¯ï¼Œè®¾ç½® false åä¼šæ²¡æœ‰å¢é‡ç¼–è¯‘æ•ˆæœ ç­›é€‰ï¼ˆå…³é”®å­—ï¼š AndroidAOP woven info codeï¼‰ build è¾“å‡ºæ—¥å¿—å¯çœ‹æ—¶é—´
    increment = true
//ä¿®æ”¹ã€å¢åŠ ã€åˆ é™¤åŒ¹é…åˆ‡é¢çš„è¯ï¼Œå°±ä¼šèµ°å…¨é‡ç¼–è¯‘
}
dependencies {
    implementation(fileTree("libs").include("*.jar", "*.aar"))
    implementation(project(":base"))
    implementation(libs.androidx.appcompat)
    implementation(libs.google.material)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.livedata.ktx)
    implementation(libs.androidx.lifecycle.viewmodel.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.compiler)
    implementation(libs.androidx.lifecycle.extensions)
    //fragment
   // implementation (libs.androidx.fragment.ktx)
    implementation(libs.androidx.navigation.fragment.ktx)
    implementation(libs.androidx.navigation.ui.ktx)
    //hiltä»£æ›¿dagger
    implementation(libs.hilt.android)
    implementation(libs.legacy.support.v4)
    ksp(libs.hilt.compiler)
    implementation(libs.glide)
    //è”ç½‘
    implementation(libs.retrofit.core)
    implementation(libs.retrofit.kotlin.serialization)
    implementation(libs.okhttp.logging)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.kotlinx.serialization.core)
    //aopæ’ä»¶
    implementation(libs.aspectj.core)
    annotationProcessor(libs.aspectj.annotation)
    //éå¿…é¡»é¡¹ ğŸ‘‡ï¼Œå¦‚æœä½ æƒ³è‡ªå®šä¹‰åˆ‡é¢éœ€è¦ç”¨åˆ°ï¼Œâš ï¸æ”¯æŒJavaå’ŒKotlinä»£ç å†™çš„åˆ‡é¢
    ksp(libs.aspectj.ksp)
    testImplementation(libs.junit4)
    androidTestImplementation(libs.androidx.test.ext)
    androidTestImplementation(libs.androidx.test.espresso.core)
}
